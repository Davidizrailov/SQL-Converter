PARM DEBUG(FLOW FLDCHK)
*
FILE PERSNL FB(150 1800)
OLD-EMP# 9 5 N
*
FILE PERSUPD CARD
EMP# 1 5 N
RAISE-PERCENT 7 2 N
*
FILE SORTPER F 150 VIRTUAL
UPD-EMP# 9 5 N
NAME 17 8 A
PAY-GROSS 94 4 P 2
NEWSAL W 4 P 2
*
FILE NEWPERS FB(150 1800)
*
FILE ERRPRINT PRINTER
*
*
SORT PERSNL TO SORTPER USING OLD-EMP#
*
JOB INPUT (SORTPER KEY(UPD-EMP#) +
PERSUPD KEY(EMP#) )
*
IF MATCHED
NEWSAL = PAY-GROSS * (1 + RAISE-PERCENT / 100)
PRINT NEW-RPT
PAY-GROSS = NEWSAL
END-IF
IF SORTPER
PUT NEWPERS FROM SORTPER
ELSE
DISPLAY ERRPRINT EMP# 'RECORD NOT MATCHED'
END-IF
*
REPORT NEW-RPT LINESIZE 80 NOPAGE NODATE
SEQUENCE NAME
TITLE 1 'SALARY UPDATE REPORT'
TITLE 2 'EMPLOYEES WITH OVER 25 YEARS SERVICE'
HEADING UPD-EMP# ('EMPL' 'NUMBER')
HEADING NAME ('EMPL' 'NAME')
HEADING PAY-GROSS ('OLD' 'SALARY')
HEADING NEWSAL ('NEW' 'SALARY')
HEADING RAISE-PERCENT ('RAISE' '%')
LINE UPD-EMP# NAME PAY-GROSS NEWSAL RAISE-PERCENT
*
END